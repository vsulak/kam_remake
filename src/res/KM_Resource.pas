unit KM_Resource;
{$I KaM_Remake.inc}
interface
uses
  {$IFDEF Unix} LCLIntf, LCLType, {$ENDIF}
  Classes, SysUtils,
  KM_CommonTypes, KM_Defaults,
  KM_ResTypes,
  KM_ResCursors,
  KM_ResFonts,
  KM_ResHouses,
  KM_ResLocales,
  KM_ResMapElements,
  KM_ResPalettes,
  KM_ResSound,
  KM_ResSprites,
  KM_ResTileset,
  KM_ResUnits,
  KM_ResWares,
  KM_ResInterpolation;


type
  TResourceLoadState = (rlsNone, rlsMenu, rlsAll); //Resources are loaded in 2 steps, for menu and the rest

  TKMResource = class
  private
    fDataState: TResourceLoadState;

    fCursors: TKMResCursors;
    fFonts: TKMResFonts;
    fHouses: TKMResHouses;
    fPalettes: TKMResPalettes;
    fUnits: TKMResUnits;
    fWares: TKMResWares;
    fSounds: TKMResSounds;
    fSprites: TKMResSprites;
    fTileset: TKMResTileset;
    fMapElements: TKMResMapElements;
    fInterpolation: TKMResInterpolation;

    procedure StepRefresh;
    procedure StepCaption(const aCaption: UnicodeString);
  public
    OnLoadingStep: TKMEvent;
    OnLoadingText: TUnicodeStringEvent;

    constructor Create(aOnLoadingStep: TKMEvent; aOnLoadingText: TUnicodeStringEvent);
    destructor Destroy; override;

    function GetDATCRC: Cardinal;

    procedure LoadMainResources(const aLocale: AnsiString = ''; aLoadFullFonts: Boolean = True);
    procedure LoadLocaleAndFonts(const aLocale: AnsiString = ''; aLoadFullFonts: Boolean = True);
    procedure LoadLocaleResources(const aLocale: AnsiString = '');
    procedure LoadGameResources(aAlphaShadows: Boolean; aForceReload: Boolean = False);
    procedure LoadLocaleFonts(const aLocale: AnsiString; aLoadFullFonts: Boolean);

    property DataState: TResourceLoadState read fDataState;
    property Palettes: TKMResPalettes read fPalettes;
    property Cursors: TKMResCursors read fCursors;
    property MapElements: TKMResMapElements read fMapElements;
    property Fonts: TKMResFonts read fFonts;
    property Sounds: TKMResSounds read fSounds;
    property Sprites: TKMResSprites read fSprites;
    property Tileset: TKMResTileset read fTileset;
    property Houses: TKMResHouses read fHouses;
    property Units: TKMResUnits read fUnits;
    property Wares: TKMResWares read fWares;
    property Interpolation: TKMResInterpolation read fInterpolation;

    procedure UpdateStateIdle;

    function IsMsgHouseUnnocupied(aMsgId: Word): Boolean;
  end;


var
  gRes: TKMResource;


implementation
uses
  TypInfo,
  {$IFNDEF NO_OGL}
  KM_System,
  {$ENDIF}
  KromUtils, KM_Log, KM_Points,
  KM_ResTexts, KM_ResKeyFuncs, KM_ResTilesetTypes;


{ TKMResource }
constructor TKMResource.Create(aOnLoadingStep: TKMEvent; aOnLoadingText: TUnicodeStringEvent);
begin
  inherited Create;

  fDataState := rlsNone;
  gLog.AddTime('Resource loading state - None');

  OnLoadingStep := aOnLoadingStep;
  OnLoadingText := aOnLoadingText;
end;


destructor TKMResource.Destroy;
begin
  FreeAndNil(fCursors);
  FreeAndNil(fHouses);
  FreeAndNil(gResLocales);
  FreeAndNil(fMapElements);
  FreeAndNil(fPalettes);
  FreeAndNil(fFonts);
  FreeAndNil(fWares);
  FreeAndNil(fSprites);
  FreeAndNil(fSounds);
  FreeAndNil(gResTexts);
  FreeAndNil(fTileset);
  FreeAndNil(fUnits);
  FreeAndNil(gResKeyFuncs);
  FreeAndNil(fInterpolation);

  inherited;
end;


procedure TKMResource.UpdateStateIdle;
begin
  fSprites.UpdateStateIdle;
end;


procedure TKMResource.StepRefresh;
begin
  if Assigned(OnLoadingStep) then OnLoadingStep;
end;


procedure TKMResource.StepCaption(const aCaption: UnicodeString);
begin
  if Assigned(OnLoadingText) then OnLoadingText(aCaption);
end;


//CRC of data files that can cause inconsitencies
function TKMResource.GetDATCRC: Cardinal;
begin
  Result := gRes.Houses.CRC xor
            fUnits.CRC xor
            fMapElements.CRC xor
            fTileset.CRC;
end;


procedure TKMResource.LoadMainResources(const aLocale: AnsiString = ''; aLoadFullFonts: Boolean = True);
var
  tileColors: TKMColor3bArray;
begin
  StepCaption('Reading palettes ...');
  fPalettes := TKMResPalettes.Create;
  //We are using only default palette in the game for now, so no need to load all palettes
  fPalettes.LoadDefaultPalette(ExeDir + 'data' + PathDelim + 'gfx' + PathDelim);
  gLog.AddTime('Reading palettes done');

  fSprites := TKMResSprites.Create(StepRefresh, StepCaption);

  fCursors := TKMResCursors.Create;

  fUnits := TKMResUnits.Create; // Load units prior to Sprites, as we could use it on SoftenShadows override for png in Sprites folder
  fSprites.LoadMenuResources;
  gLog.AddTime('LoadMenuResources done');

  {$IFNDEF NO_OGL}
  gLog.AddTime('MakeCursors start');
  gSystem.MakeCursors(fSprites[rxGui]);
  gLog.AddTime('MakeCursors done');
  gSystem.Cursor := kmcDefault;
  {$ENDIF}
  fCursors.SetRXDataPointer(@fSprites[rxGui].RXData);

  gResKeyFuncs := TKMResKeyFuncs.Create;
  gLog.AddTime('LoadLocaleAndFonts start');
  LoadLocaleAndFonts(aLocale, aLoadFullFonts);
  gLog.AddTime('LoadLocaleAndFonts done');

  fTileset := TKMResTileset.Create;
  if not SKIP_RENDER then
  begin
    if fSprites.Sprites[rxTiles].RXData.Count <> TILES_CNT then
      gLog.AddTime('fSprites.Sprites[rxTiles].RXData.Count = ' + IntToStr(fSprites.Sprites[rxTiles].RXData.Count));

    gLog.AddTime('GetAverageSpriteColors start');
    tileColors := fSprites.Sprites[rxTiles].GetAverageSpriteColors(TILES_CNT);
    fTileset.SetTileColors(tileColors);
    gLog.AddTime('GetAverageSpriteColors done');
  end;

  gLog.AddTime('LoadMapElements start');
  fMapElements := TKMResMapElements.Create;
  fMapElements.LoadFromFile(ExeDir + 'data' + PathDelim + 'defines' + PathDelim + 'mapelem.dat');
  gLog.AddTime('LoadMapElements done');

  fSprites.ClearTemp;
  gLog.AddTime('ClearTemp done');

  fWares := TKMResWares.Create;
  gLog.AddTime('ResWares created');
  fHouses := TKMResHouses.Create;
  gLog.AddTime('ResHouses created');

  StepRefresh;
  gLog.AddTime('ReadGFX is done');
  fDataState := rlsMenu;
  gLog.AddTime('Resource loading state - Menu');
end;


procedure TKMResource.LoadLocaleResources(const aLocale: AnsiString = '');
begin
  FreeAndNil(gResLocales);
  FreeAndNil(gResTexts);
  FreeAndNil(fSounds);

  gLog.AddTime('LoadLocaleResources: creating locales');
  gResLocales := TKMResLocales.Create(ExeDir + 'data' + PathDelim + 'locales.txt', aLocale);

  gLog.AddTime('LoadLocaleResources: loading texts');
  gResTexts := TKMTextLibraryMulti.Create;
  gResTexts.LoadLocale(ExeDir + 'data' + PathDelim + 'text' + PathDelim + 'text.%s.libx', False);

  gLog.AddTime('LoadLocaleResources: creating sounds');
  fSounds := TKMResSounds.Create(gResLocales.UserLocale, gResLocales.FallbackLocale, gResLocales.DefaultLocale);
  gLog.AddTime('LoadLocaleResources: done');
end;


procedure TKMResource.LoadLocaleAndFonts(const aLocale: AnsiString = ''; aLoadFullFonts: Boolean = True);
begin
  // Locale info is needed for DAT export and font loading
  LoadLocaleResources(aLocale);

  StepCaption('Reading fonts ...');
  gLog.AddTime('LoadLocaleAndFonts: creating fonts');
  fFonts := TKMResFonts.Create;
  gLog.AddTime('LoadLocaleAndFonts: loading fonts (full=%s)', [BoolToStr(aLoadFullFonts, True)]);
  if aLoadFullFonts or gResLocales.LocaleByCode(aLocale).NeedsFullFonts then
    fFonts.LoadFonts(fllFull)
  else
    fFonts.LoadFonts(fllMinimal);
  gLog.AddTime('Read fonts is done');
end;


procedure TKMResource.LoadLocaleFonts(const aLocale: AnsiString; aLoadFullFonts: Boolean);
begin
  if (Fonts.LoadLevel <> fllFull)
    and (aLoadFullFonts or gResLocales.LocaleByCode(aLocale).NeedsFullFonts) then
    Fonts.LoadFonts(fllFull);
end;


procedure TKMResource.LoadGameResources(aAlphaShadows: Boolean; aForceReload: Boolean = False);
var
  doForceReload: Boolean;
begin
  if fInterpolation = nil then
  begin
    gLog.AddTime('LoadGameResources ... Interpolations from interp.dat');
    fInterpolation := TKMResInterpolation.Create;
    fInterpolation.LoadFromFile(ExeDir + 'data' + PathDelim + 'defines' + PathDelim + 'interp.dat');
  end;

  gLog.AddTime('LoadGameResources ... AlphaShadows: ' + BoolToStr(aAlphaShadows, True) + '. Forced: ' + BoolToStr(aForceReload, True));
  doForceReload := aForceReload or (aAlphaShadows <> fSprites.AlphaShadows);
  if (fDataState <> rlsAll)
    {$IFDEF LOAD_GAME_RES_ASYNC}or not fSprites.GameResLoadCompleted {$ENDIF}
    or doForceReload then
  begin
    // Load game Reources
    // TempData is cleared while loading GameResources (after each step)
    fSprites.LoadGameResources(aAlphaShadows, doForceReload);

    fDataState := rlsAll;
  end;

  gLog.AddTime('Resource loading state - Game');
end;


function TKMResource.IsMsgHouseUnnocupied(aMsgId: Word): Boolean;
begin
  Result := (aMsgId >= TX_MSG_HOUSE_UNOCCUPIED__22) and (aMsgId <= TX_MSG_HOUSE_UNOCCUPIED__22 + 22);
end;


end.
